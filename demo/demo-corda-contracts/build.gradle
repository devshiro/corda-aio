buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases'
        }
    }
    dependencies {
        classpath "net.corda.plugins:cordapp:${CORDAPP_PLUGINS_VERSION}"
        classpath "net.corda.plugins:cordformation:${CORDAPP_PLUGINS_VERSION}"
        classpath "net.corda.plugins:quasar-utils:${CORDAPP_PLUGINS_VERSION}"
    }
}



apply plugin: JavaModuleBasePlugin
apply plugin: 'net.corda.plugins.cordapp'

// todo: All corda related config and dependency stuff should be moved to separate plugin
configurations {
    compile {
        // We want to use SLF4J's version of these bindings: jcl-over-slf4j
        // Remove any transitive dependency on Apache's version.
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-parameters" // Required for shell commands.
}

jar {
    // CorDapps do not configure a Node's logging.
    exclude '**/log4j2*.xml'
    baseName 'demo-cordapp-contracts'
}

cordapp {
    targetPlatformVersion 5
    minimumPlatformVersion 4
    contract {
        name "Demo Contract"
        vendor "DevShiro"
        licence "TBD"
        versionId 1
    }
    sealing {
        enabled false
    }
}

dependencies {
    cordaCompile "net.corda:corda-core:${CORDA_VERSION}"
    compile("com.google.guava:guava:${GUAVA_VERSION}")
    compile project(":framr-annotations")
}