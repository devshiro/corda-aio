import de.undercouch.gradle.tasks.download.Download

plugins {
    id "de.undercouch.download"
}

task downloadNetworkBootstrapperTool(type: Download) {
    src "https://software.r3.com/artifactory/corda-releases/net/corda/corda-tools-network-bootstrapper/4.3/corda-tools-network-bootstrapper-4.3.jar"
    dest buildDir
}

task copyCordapps(type: Copy) {
    from "${rootDir}/demo/demo-corda-contracts/build/libs/demo-cordapp-contracts-0.1.jar"
    from "${rootDir}/demo/demo-corda-workflows/build/libs/demo-cordapp-workflows-0.1.jar"
    into("${buildDir}/nodes/")
}

task copyNodeConfs(type: Copy) {
    from "${projectDir}/src/"
    into "${buildDir}/nodes/"
}

task bootstrapNetwork(type: Exec) {
    workingDir buildDir
    commandLine 'java', '-jar', 'corda-tools-network-bootstrapper-4.3.jar', '--dir', 'nodes/'
}

task prepareNetwork()

copyCordapps.dependsOn(":demo:demo-corda-contracts:build")
copyCordapps.dependsOn(":demo:demo-corda-workflows:build")

prepareNetwork.dependsOn copyCordapps
prepareNetwork.dependsOn copyNodeConfs
prepareNetwork.dependsOn downloadNetworkBootstrapperTool

bootstrapNetwork.dependsOn prepareNetwork